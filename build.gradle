/*
 * Copyright 2019 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    
    dependencies {
        classpath 'com.netflix.nebula:gradle-netflixoss-project-plugin:5.1.1'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}

ext {
    junitVersion = '5.3.+'
    mockitoVersion = '2.18.+'
    slf4jVersion = '1.7.+'
    jacksonVersion = '2.10.+'
    spectatorVersion = 'latest.release'
    archaiusVersion = 'latest.release'
}

allprojects {
    apply plugin: 'nebula.netflixoss'
    apply plugin: 'idea'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'com.github.johnrengelman.shadow'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    group = 'io.mantisrx'

    if (project.hasProperty('useMavenLocal')) {
        repositories {
            mavenLocal()
        }
    }

    repositories {
        jcenter()
    }

    dependencies {
        implementation "org.slf4j:slf4j-api:$slf4jVersion"

        testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
        testImplementation "org.mockito:mockito-core:$mockitoVersion"
        testImplementation "org.slf4j:slf4j-log4j12:$slf4jVersion"
    }

    test {
        useJUnitPlatform()
    }

    tasks.withType(Javadoc).all {
        enabled = false
    }

    license {
        ignoreFailures = false
    }
}

apply from: file('gradle/check.gradle')
